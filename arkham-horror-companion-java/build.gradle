plugins {
	id 'java'
	id 'eclipse'
	id 'war'
	id 'jacoco'
	id 'checkstyle'
	id "com.github.spotbugs" version "4.7.2"
}

group = 'com.wanderingwyatt'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

repositories {
	mavenCentral()
}

dependencies {
	compileOnly project(':arkham-horror-companion-annotations')
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '_'
  	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '_'
	// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j-impl
	implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '_'
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '_'
	// https://mvnrepository.com/artifact/org.hibernate/hibernate-core
	implementation group: 'org.hibernate.orm', name: 'hibernate-core', version: '_'
	// https://mvnrepository.com/artifact/org.hibernate.orm/hibernate-jcache
	implementation group: 'org.hibernate.orm', name: 'hibernate-jcache', version: '_'
	// https://mvnrepository.com/artifact/javax.cache/cache-api
	implementation group: 'javax.cache', name: 'cache-api', version: '_'
	// https://mvnrepository.com/artifact/org.ehcache/ehcache
	implementation group: 'org.ehcache', name: 'ehcache', version: '_'
	// https://mvnrepository.com/artifact/javax.annotation/javax.annotation-api
	implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '_'
	// https://mvnrepository.com/artifact/com.google.dagger/dagger
	implementation group: 'com.google.dagger', name: 'dagger', version: '_'
	// https://mvnrepository.com/artifact/org.aeonbits.owner/owner
	implementation group: 'org.aeonbits.owner', name: 'owner', version: '_'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '_'



	// https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
	annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '_'
	annotationProcessor project(':arkham-horror-companion-annotation-processor')
	testAnnotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '_'
	
	providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '_'
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:_"
	testImplementation "org.mockito:mockito-core:_"
	testImplementation "org.mockito:mockito-junit-jupiter:_"
	// https://mvnrepository.com/artifact/com.h2database/h2
	testImplementation group: 'com.h2database', name: 'h2', version: '_'

	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:_"
	testRuntimeOnly "org.apache.logging.log4j:log4j-jul:_"
}

eclipse {
	classpath {
		file.whenMerged { cp ->
			org.gradle.plugins.ide.eclipse.model.SourceFolder generatedMainSource = new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main', null)
			generatedMainSource.entryAttributes['ignore_optional_problems'] = 'true'
			org.gradle.plugins.ide.eclipse.model.SourceFolder generatedTestSource = new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/test', 'bin/generated/test')
			generatedTestSource.entryAttributes['ignore_optional_problems'] = 'true'
			generatedTestSource.entryAttributes['test'] = 'true'
			cp.entries.add(generatedMainSource)
			cp.entries.add(generatedTestSource)
		}
	}
}

compileJava {
	options.compilerArgs += ["-Aarkham.cache.packageName=com.wanderingwyatt.arkham.modules.generated"]
}

ext {
	jacocoExclusions = ['**/com/wanderingwyatt/arkham/modules/**',
						'**/com/wanderingwyatt/arkham/components/**',
						'**/com/wanderingwyatt/arkham/**/*Exception*']
}

jacocoTestReport {
	dependsOn test
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
        	fileTree(dir: it, exclude: jacocoExclusions)
        }))
    }
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.8000
			}
		}
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
        	fileTree(dir: it, exclude: jacocoExclusions)
        }))
    }
}

spotbugsMain {
	reports {
		html {
			enabled = true
		}
	}
}

spotbugs {
	reportLevel = "high"
}

test {
	useJUnitPlatform()
	systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
	finalizedBy jacocoTestReport
}

war {
	dependsOn project(':arkham-horror-companion-app').yarn_build
	from project(':arkham-horror-companion-app').buildDir
}
